plugins {
    id 'java'
    id 'maven-publish'
}

group 'com.github.noonmaru.collections'
version '1.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'
javadoc.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

publishing {
    publications {
        MoxPublication(MavenPublication) {
            artifactId = 'collections'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'Noonmaru'
                description = 'Noonmaru collection library'
                url = 'https://github.com/noonmaru/collections'
                licenses {
                    license {
                        name = 'GPL-2.0'
                        url = 'https://opensource.org/licenses/gpl-2.0.php'
                    }
                }
                developers {
                    developer {
                        id = 'noonmaru'
                        name = 'Noonmaru'
                        email = 'noonmaru10@gmail.com'
                        url = 'https://github.com/noonmaru'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/noonmaru/collections.git'
                    developerConnection = 'scm:git:ssh://github.com/noonmaru/collections.git'
                    url = 'https://github.com/noonmaru/collections'
                }
            }
        }
    }
}